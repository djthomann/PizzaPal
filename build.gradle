plugins {
    id 'application'
    id "org.openjfx.javafxplugin" version "0.1.0"
}


application {
    mainClass = "de.hsrm.mi.swt.LagerMain"
}

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(21) // Ensure this matches your Java version
    }
    sourceCompatibility = '21'
}

// Konfiguration des JavaFX-Plugins: Version und benötigte Module
javafx {
    version = "21.0.3"
    modules = [ 'javafx.controls', 'javafx.fxml' ]
}

// Abzufragendes Internet-Repository für Bibliotheken (z.B. JavaFX)
repositories { 
    mavenCentral() 
}

// Abhängigkeiten (benötigte und einzubindende Bibliotheken)
dependencies {
    testImplementation 'org.junit.jupiter:junit-jupiter:5.10.2'
    testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
    implementation 'com.google.code.gson:gson:2.8.8'
    implementation 'com.fasterxml.jackson.core:jackson-databind:2.0.1'
}

// Initialisierung für JUnit-Testing
test {
    useJUnitPlatform()
    jvmArgs(['--enable-preview'])
}

/* ./gradlew fatJar erzeugt Standalone-JAR (App incl. benoetigter Bibliotheken)
 * Datei wird in Ordner build/libs angelegt
 * Dateiname ist letzte Komponente von application.mainClass (s.o.) + Endung ".jar"
 * Ausfuehrung mit java -jar <dateiname>
 * Mehr Infos: https://www.baeldung.com/gradle-fat-jar
 */
task fatJar(type: Jar) {
    manifest {
        attributes 'Main-Class': application.mainClass
    }
    archiveBaseName = application.mainClass.get().split('\\.').last()

    duplicatesStrategy = DuplicatesStrategy.EXCLUDE
    from { configurations.runtimeClasspath.collect { it.isDirectory() ? it : zipTree(it) } }
    with jar
}

tasks.withType(JavaCompile).each {
    it.options.compilerArgs.add('--enable-preview')
}